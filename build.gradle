import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.xmlobjects'
version '0.9.1'
description 'A simple and lightweight XML-to-object mapping library'
ext.date = new Date()

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

repositories {
    jcenter()
}

dependencies {
    api 'org.glassfish.jaxb:xsom:2.3.2'
    api 'org.atteo.classindex:classindex:3.9'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.1.1'
    annotationProcessor 'org.atteo.classindex:classindex:3.9'
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "xml-objects $project.version"
        bottom """
            <a target="_blank" href="https://github.com/xmlobjects/xml-objects/issues">Report a bug or suggest an enhancement</a><br>
            xml-objects is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
            Official website and more information at: <a target="_blank" href="https://github.com/xmlobjects">https://github.com/xmlobjects</a><br>
            Copyright &copy; 2013-${date.format('yyyy')}<br>
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/xmlobjects/xml-objects'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/xmlobjects/xml-objects'
                    connection = 'https://github.com/xmlobjects/xml-objects.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/xmlobjects/xml-objects'
        vcsUrl = 'https://github.com/xmlobjects/xml-objects.git'
        issueTrackerUrl = 'https://github.com/xmlobjects/xml-objects/issues'
        labels = ['java', 'xml', 'object', 'mapping', 'parsing', 'writing']
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}