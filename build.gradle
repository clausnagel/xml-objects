import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.xml-objects'
version '0.3.0'
description 'A simple and lightweight XML-to-object mapping library'

sourceCompatibility = 1.8

ext {
    date = new Date()
}

repositories {
    jcenter()
}

dependencies {
    api 'org.glassfish.jaxb:xsom:2.3.2'
    api 'org.atteo.classindex:classindex:3.8'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.0.3'
    annotationProcessor 'org.atteo.classindex:classindex:3.8'
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "xml-objects $project.version"
        bottom """
            <a target="_blank" href="https://github.com/clausnagel/xml-objects/issues">Report a bug or suggest an enhancement</a><br>
            xml-objects is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
            Official website and more information at: <a target="_blank" href="https://github.com/clausnagel/xml-objects">https://github.com/clausnagel/xml-objects</a><br>
            Copyright &copy; 2013-${date.format('yyyy')}<br>
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/clausnagel/xml-objects'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/clausnagel/xml-objects'
                    connection = 'scm:git:https://github.com/clausnagel/xml-objects.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/clausnagel/xml-objects'
        vcsUrl = 'https://github.com/clausnagel/xml-objects.git'
        issueTrackerUrl = 'https://github.com/clausnagel/xml-objects/issues'
        labels = ['xml', 'object', 'mapping', 'parsing', 'writing']
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}